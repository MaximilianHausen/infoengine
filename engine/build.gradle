plugins {
    id 'java-library'
    id 'maven-publish'
}

group = 'org.totodev'
version = '0.1'

project.ext.lwjglVersion = '3.3.1'

repositories {
    mavenCentral()
}

java {
    withSourcesJar()
    withJavadocJar()
    registerFeature('x64windows') {
        usingSourceSet(sourceSets.main)
    }
    registerFeature('x86windows') {
        usingSourceSet(sourceSets.main)
    }
    registerFeature('arm64windows') {
        usingSourceSet(sourceSets.main)
    }
    registerFeature('x64linux') {
        usingSourceSet(sourceSets.main)
    }
    registerFeature('arm64linux') {
        usingSourceSet(sourceSets.main)
    }
    registerFeature('arm32linux') {
        usingSourceSet(sourceSets.main)
    }
    registerFeature('x64macos') {
        usingSourceSet(sourceSets.main)
    }
    registerFeature('arm64macos') {
        usingSourceSet(sourceSets.main)
    }
}

dependencies {
    ext.allPlatformsNative = { baseDependency ->
        x64windowsRuntimeOnly "$baseDependency:natives-windows"
        x86windowsRuntimeOnly "$baseDependency:natives-windows-x86"
        arm64windowsRuntimeOnly "$baseDependency:natives-windows-arm64"

        x64linuxRuntimeOnly "$baseDependency:natives-linux"
        arm64linuxRuntimeOnly "$baseDependency:natives-linux-arm64"
        arm32linuxRuntimeOnly "$baseDependency:natives-linux-arm32"

        x64macosRuntimeOnly "$baseDependency:natives-macos"
        arm64macosRuntimeOnly "$baseDependency:natives-macos-arm64"
    }

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'

    implementation 'com.google.code.gson:gson:2.9.0'

    implementation 'org.eclipse.collections:eclipse-collections:11.0.0'
    api 'org.eclipse.collections:eclipse-collections-api:11.0.0'
    api 'org.jetbrains:annotations:23.0.0'
    api 'org.joml:joml:1.10.4'

    // lwjgl
    implementation "org.lwjgl:lwjgl:$lwjglVersion"
    implementation "org.lwjgl:lwjgl-assimp:$lwjglVersion"
    implementation "org.lwjgl:lwjgl-glfw:$lwjglVersion"
    implementation "org.lwjgl:lwjgl-openal:$lwjglVersion"
    implementation "org.lwjgl:lwjgl-stb:$lwjglVersion"

    // lwjgl natives
    allPlatformsNative "org.lwjgl:lwjgl:$lwjglVersion"
    allPlatformsNative "org.lwjgl:lwjgl-assimp:$lwjglVersion"
    allPlatformsNative "org.lwjgl:lwjgl-glfw:$lwjglVersion"
    allPlatformsNative "org.lwjgl:lwjgl-openal:$lwjglVersion"
    allPlatformsNative "org.lwjgl:lwjgl-stb:$lwjglVersion"

    // vulkan + natives for each capability
    implementation(project(':vulkan'))
    x64windowsImplementation(project(':vulkan')) {
        capabilities {
            requireCapability 'org.totodev:vulkan-x64windows'
        }
    }
    x86windowsImplementation(project(':vulkan')) {
        capabilities {
            requireCapability 'org.totodev:vulkan-x86windows'
        }
    }
    arm64windowsImplementation(project(':vulkan')) {
        capabilities {
            requireCapability 'org.totodev:vulkan-arm64windows'
        }
    }
    x64linuxImplementation(project(':vulkan')) {
        capabilities {
            requireCapability 'org.totodev:vulkan-x64linux'
        }
    }
    arm64linuxImplementation(project(':vulkan')) {
        capabilities {
            requireCapability 'org.totodev:vulkan-arm64linux'
        }
    }
    arm32linuxImplementation(project(':vulkan')) {
        capabilities {
            requireCapability 'org.totodev:vulkan-arm32linux'
        }
    }
    x64macosImplementation(project(':vulkan')) {
        capabilities {
            requireCapability 'org.totodev:vulkan-x64macos'
        }
    }
    arm64macosImplementation(project(':vulkan')) {
        capabilities {
            requireCapability 'org.totodev:vulkan-arm64macos'
        }
    }
}

publishing {
    publications {
        release(MavenPublication) {
            from components.java
            pom {
                name = 'Infoengine'
                version = project.getVersion()
                description = 'Java game engine with a focus on flexibility and correctness'
                url = 'https://github.com/MaximilianHausen/infoengine'
                licenses {
                    license {
                        name = 'Mozilla Public License 2.0'
                        url = 'https://www.mozilla.org/en-US/MPL/2.0/'
                    }
                }
            }
        }
        snapshot(MavenPublication) {
            from components.java
            pom {
                name = 'Infoengine'
                version = project.getVersion() + '-SNAPSHOT'
                description = 'Java game engine with a focus on flexibility and correctness'
                url = 'https://github.com/MaximilianHausen/infoengine'
                licenses {
                    license {
                        name = 'Mozilla Public License 2.0'
                        url = 'https://www.mozilla.org/en-US/MPL/2.0/'
                    }
                }
            }
        }
    }

    repositories {
        maven {
            name = 'release'
            url = uri("https://maven.totodev.de/releases")
            credentials {
                username = System.getenv("MAVEN_USERNAME")
                password = System.getenv("MAVEN_PASSWORD")
            }
        }
        maven {
            name = 'snapshot'
            url = uri("https://maven.totodev.de/snapshots")
            credentials {
                username = System.getenv("MAVEN_USERNAME")
                password = System.getenv("MAVEN_PASSWORD")
            }
        }
    }
}

tasks.withType(PublishToMavenRepository) {
    onlyIf {
        (repository == publishing.repositories.release && publication == publishing.publications.release) ||
        (repository == publishing.repositories.snapshot && publication == publishing.publications.snapshot)
    }
}

test {
    useJUnitPlatform()
}
