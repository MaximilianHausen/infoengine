plugins {
    id 'java-library'
    id 'maven-publish'
}

group = 'net.totodev'
version = '0.1'

project.ext.lwjglVersion = '3.3.1'

repositories {
    mavenCentral()
}

java {
    withSourcesJar()
    withJavadocJar()
    registerFeature('x64windows') {
        usingSourceSet(sourceSets.main)
    }
    registerFeature('x86windows') {
        usingSourceSet(sourceSets.main)
    }
    registerFeature('arm64windows') {
        usingSourceSet(sourceSets.main)
    }
    registerFeature('x64linux') {
        usingSourceSet(sourceSets.main)
    }
    registerFeature('arm64linux') {
        usingSourceSet(sourceSets.main)
    }
    registerFeature('arm32linux') {
        usingSourceSet(sourceSets.main)
    }
    registerFeature('x64macos') {
        usingSourceSet(sourceSets.main)
    }
    registerFeature('arm64macos') {
        usingSourceSet(sourceSets.main)
    }
}

dependencies {
    ext.allPlatformNative = { baseDependency ->
        x64windowsRuntimeOnly "$baseDependency:natives-windows"
        x86windowsRuntimeOnly "$baseDependency:natives-windows-x86"
        arm64windowsRuntimeOnly "$baseDependency:natives-windows-arm64"

        x64linuxRuntimeOnly "$baseDependency:natives-linux"
        arm64linuxRuntimeOnly "$baseDependency:natives-linux-arm64"
        arm32linuxRuntimeOnly "$baseDependency:natives-linux-arm32"

        x64macosRuntimeOnly "$baseDependency:natives-macos"
        arm64macosRuntimeOnly "$baseDependency:natives-macos-arm64"
    }

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'

    implementation 'com.google.code.gson:gson:2.9.0'
    //implementation project(':vulkan')

    api 'org.eclipse.collections:eclipse-collections-api:11.0.0'
    api 'org.eclipse.collections:eclipse-collections:11.0.0'
    api 'org.jetbrains:annotations:23.0.0'
    api 'org.joml:joml:1.10.4'

    implementation "org.lwjgl:lwjgl:$lwjglVersion"
    implementation "org.lwjgl:lwjgl-assimp:$lwjglVersion"
    implementation "org.lwjgl:lwjgl-glfw:$lwjglVersion"
    implementation "org.lwjgl:lwjgl-openal:$lwjglVersion"
    implementation "org.lwjgl:lwjgl-stb:$lwjglVersion"
    implementation "org.lwjgl:lwjgl-vulkan:$lwjglVersion"

    allPlatformNative "org.lwjgl:lwjgl:$lwjglVersion"
    allPlatformNative "org.lwjgl:lwjgl-assimp:$lwjglVersion"
    allPlatformNative "org.lwjgl:lwjgl-glfw:$lwjglVersion"
    allPlatformNative "org.lwjgl:lwjgl-openal:$lwjglVersion"
    allPlatformNative "org.lwjgl:lwjgl-stb:$lwjglVersion"
}

publishing {
    publications {
        snapshot(MavenPublication) {
            from components.java
            pom {
                name = 'Infoengine'
                version = project.getVersion() + '-SNAPSHOT'
                description = 'Java game engine with a focus on flexibility and correctness'
                url = 'https://github.com/MaximilianHausen/infoengine'
                licenses {
                    license {
                        name = 'Mozilla Public License 2.0'
                        url = 'https://www.mozilla.org/en-US/MPL/2.0/'
                    }
                }
            }
        }
        release(MavenPublication) {
            from components.java
            pom {
                name = 'Infoengine'
                version = project.getVersion()
                description = 'Java game engine with a focus on flexibility and correctness'
                url = 'https://github.com/MaximilianHausen/infoengine'
                licenses {
                    license {
                        name = 'Mozilla Public License 2.0'
                        url = 'https://www.mozilla.org/en-US/MPL/2.0/'
                    }
                }
            }
        }
    }

    repositories {
        maven {
            name = 'snapshot'
            url = uri("https://maven.totodev.de/snapshots")
            credentials {
                username = System.getenv("MAVEN_USERNAME")
                password = System.getenv("MAVEN_PASSWORD")
            }
        }
        maven {
            name = 'release'
            url = uri("https://maven.totodev.de/releases")
            credentials {
                username = System.getenv("MAVEN_USERNAME")
                password = System.getenv("MAVEN_PASSWORD")
            }
        }
    }
}

tasks.withType(PublishToMavenRepository) {
    onlyIf {
        (repository == publishing.repositories.snapshot && publication == publishing.publications.snapshot) ||
        (repository == publishing.repositories.release && publication == publishing.publications.release)
    }
}

test {
    useJUnitPlatform()
}